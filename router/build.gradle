plugins {
    id 'java-library'
}

sourceSets {
    main {
        java {
            srcDir 'java/src'
        }
    }
    test {
        java {
            srcDir 'java/test/junit'
            exclude {
                it.name.endsWith('IT.java')
            }
            // Needs rewriting
            exclude 'net/i2p/router/tunnel/BuildMessageTestStandalone.java'
        }
    }
}

dependencies {
    api project(':core')
    implementation 'gnu.getopt:java-getopt:1.0.13'
    testImplementation project(path: ':core', configuration: 'tests')
}

jar {
    manifest {
        attributes 'Specification-Title': 'I2P Router'
        attributes 'Implementation-Title': 'I2P Java Router'
        attributes 'Main-Class': 'net.i2p.router.CommandLine'
        // so people with very old wrapper.config files will still work with Jetty 6
        attributes 'Class-Path': 'jetty-i2p.jar jetty-java5-threadpool.jar jetty-rewrite-handler.jar jetty-sslengine.jar jetty-start.jar jetty-util.jar'
    }
    from ('resources', {
        into "net/i2p/router/util/resources"
    })
}
