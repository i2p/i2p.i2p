apply plugin: 'war'

sourceSets {
    main {
        java {
            srcDir 'java/src'
        }
    }
    test {
        java {
            srcDir 'java/test/junit'
        }
    }
}

dependencies {
    compile project(':core')
    compile project(':apps:ministreaming')
    compile 'gnu.getopt:java-getopt:1.0.13'
    providedCompile project(':apps:jetty')
}

task i2ptunnelJar(type: Jar) {
    from sourceSets.main.output 
    exclude '**/ui/*.class'
    exclude '**/EditBean.class'
    exclude '**/IndexBean.class'
    manifest {
        attributes 'Main-Class': 'net.i2p.i2ptunnel.I2PTunnel'
        attributes 'Class-Path': 'i2p.jar mstreaming.jar'
    }
}

task tempBeansJar(type: Jar) {
    from sourceSets.main.output
    include '**/EditBean.class'
    include '**/ui/*.class'
    include '**/IndexBean.class'
    baseName = 'temp-beans'
}

task uiJar(type: Jar) {
    baseName = 'i2ptunnel-ui'
    from sourceSets.main.output
    include '**/ui/*.class'
    manifest {
        attributes 'Class-Path' : 'i2p.jar mstreaming.jar i2ptunnel.jar'
    }
}

artifacts {
    archives i2ptunnelJar, tempBeansJar, uiJar
}

war {
    from 'jsp'
    exclude 'jsp/web.xml'
    webXml = file('jsp/web.xml')
}

